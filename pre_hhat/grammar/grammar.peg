program = protocols? funcs* main? EOF
protocols = 'protocol' ':' (id / STR / ( '(' type ':' (id / STR) (',' type ':' (id / STR) )* ')' ))
builtin = 'add' / 'print' / '@x' / '@h' / '@cnot'
logic_builtin = 'eq' / 'neq' / 'and' / 'or' / 'not' / 'gt' / 'gte' / 'lt' / 'lte'
funcs = 'func' type_expr id (( '(' params? ')' )/ ':' ) '(' body* return? ')'
params =  type_expr id (type_expr id)*
main = 'main' '(' body* ')'
body = gen_call / var_decl / var_assign / cond
var_decl = type_expr id ('=' (assign / expr) )?
type_expr = (type / id) '{' expr? '}'
type = 'int' / 'str' / 'hashmap' / 'circuit' / 'null' / 'bool'
var_assign = id assign
gen_call = collect? (builtin / id) (args/args2)
assign = ( '(' assign_expr ((',' assign_expr) / pipe)* ')' )
assign_expr = index_expr ':' expr
index_expr = expr?
pipe = ('|>' (var_assign / assign_expr / expr) )
expr = BOOL / INT / STR / HASHMAP / caller / cond / ( '(' expr+ ')' ) / id
caller = collect? ((builtin args?) / (logic_builtin args ) / (id (args/args2)))
args = '(' expr* ')'
args2 = '(' id ':' expr (',' id ':' expr)* ')'
collect = 'collect'
cond = 'if' '(' cond_expr (',' cond_expr)* ')'
cond_expr = (( caller ('as' id)? ) / true_cond ) ':' (body / ( '(' body* ')' ))
true_cond = 'T'
return = 'return' ( ( '(' expr* ')' ) / expr)
id = r'(@)?[a-zA-Z][a-zA-Z_\-0-9]*'
BOOL = r'T|F'
INT = r'(\-)?[0-9]+'
STR = r'(\'(.+?)\'|\"(.+?)\")'
HASHMAP = '+(' hash_expr (',' hash_expr )* ')'
hash_expr = (id / INT / STR) ':' (id / INT / STR)
